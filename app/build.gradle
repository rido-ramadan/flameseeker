apply plugin: 'com.android.application'
apply plugin: 'idea'

def jsonFile = rootProject.file("app/google-services.json")
def servicesJSON = new groovy.json.JsonSlurper().parseText(jsonFile.text)

def firebaseKeyFile = rootProject.file("app/firebase.properties")
def firebaseProperties = new Properties()
firebaseProperties.load(new FileInputStream(firebaseKeyFile))

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "com.edgardrake.flameseeker"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
//    signingConfigs {
//        config {
//            keyAlias keystoreProperties['keyAlias']
//            keyPassword keystoreProperties['keyPassword']
//            storeFile file(keystoreProperties['storeFile'])
//            storePassword keystoreProperties['storePassword']
//        }
//    }
    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
    }
    dexOptions {
        javaMaxHeapSize "4g"
        maxProcessCount 4
    }
    buildTypes {
        debug {
            resValue "string", "app_key", servicesJSON.client[0].api_key[0].current_key
            resValue "string", "server_key", firebaseProperties['server_key']
        }
        release {
            resValue "string", "app_key", servicesJSON.client[0].api_key[0].current_key
            resValue "string", "server_key", firebaseProperties['server_key']
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

idea {
    module {
        excludeDirs = [file('build')]
    }
}

ext {
    supportLibraryVersion = '26.1.0'
    playServicesVersion = '11.4.2'
    firebaseVersion = '11.4.2'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    testCompile 'junit:junit:4.12'

    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:gridlayout-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.google.android.gms:play-services-maps:$playServicesVersion"
    compile "com.google.android.gms:play-services-auth:$playServicesVersion"
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile 'com.android.support:multidex:1.0.2'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.oguzdev:CircularFloatingActionMenu:1.0.2'
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'
    compile 'com.google.dagger:dagger:2.12'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'
    compile 'com.google.dagger:dagger-android-support:2.12' // if you use the support libraries
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.12'
}

apply plugin: 'com.google.gms.google-services'